#pragma kernel Update
#pragma kernel InitBackground

RWTexture2D<float4> _Canvas;
float _CanvasWidth;
float _CanvasHeight;
float4 _PreviousMousePosition;
float4 _MousePosition;
float _StrokeSmoothingInterval;
bool _MouseDown;
float _BrushSize;
float4 _BrushColour;

float4 HardBrush(float2 pixelPos, float4 currentColor, float4 brushColor, float brushSize, float4 previousMousePosition,
                 float4 mousePosition, float strokeSmoothingInterval)
{
    for (float i = 0; i < 1.0; i += strokeSmoothingInterval)
    {
        const float2 mousePos = lerp(previousMousePosition, mousePosition, i);
        if (length(pixelPos - mousePos) < brushSize)
            return brushColor;
    }
    return currentColor;
}

[numthreads(8,8,1)]
void InitBackground(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _CanvasWidth || id.y >= _CanvasHeight)
        return;

    _Canvas[id.xy] = float4(1, 1, 1, 0);
}

[numthreads(8,8,1)]
void Update(uint3 id : SV_DispatchThreadID)
{
    if (!_MouseDown)
        return;

    if (id.x >= _CanvasWidth || id.y >= _CanvasHeight)
        return;

    _Canvas[id.xy] = HardBrush(id.xy, _Canvas[id.xy], _BrushColour, _BrushSize,
                               _PreviousMousePosition, _MousePosition, _StrokeSmoothingInterval);
}
